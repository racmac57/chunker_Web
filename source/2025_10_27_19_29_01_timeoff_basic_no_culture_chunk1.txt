let
    FolderPath = "C:\Users\carucci_r\OneDrive - City of Hackensack\_MONTHLY_DATA\_EXPORTS\_POSS_EXPORT\TIME_OFF_EXPORT",
    
    Source = Folder.Files(FolderPath),
    FilteredFiles = Table.SelectRows(Source, each 
        [Attributes]?[Hidden]? <> true and 
        (Text.EndsWith([Name], ".xlsx") or Text.EndsWith([Name], ".xls"))
    ),
    
    TransformFiles = Table.AddColumn(FilteredFiles, "Data", each
        try 
            let
                ExcelFile = Excel.Workbook([Content], null, true),
                TimeOffActivity = ExcelFile{[Name="TimeOffActivity"]}[Data],
                PromotedHeaders = Table.PromoteHeaders(TimeOffActivity, [PromoteAllScalars=true]),
                
                // Split Times column at the "-" 
                SplitTimes = Table.SplitColumn(PromotedHeaders, "Times", Splitter.SplitTextByDelimiter("-", QuoteStyle.Csv), {"START_TIME", "END_TIME"}),
                
                // Add basic date/time types
                ChangedTypes = Table.TransformColumnTypes(SplitTimes,{
                    {"Date", type date},
                    {"START_TIME", type time},
                    {"END_TIME", type time},
                    {"Hours", type number}
                }),
                
                // Clean text fields
                TrimmedFields = Table.TransformColumns(ChangedTypes,{
                    {"Group", Text.Trim, type text}, 
                    {"Employee", Text.Trim, type text}, 
                    {"Reason", Text.Trim, type text}, 
                    {"Status", Text.Trim, type text},
                    {"Shift", Text.Trim, type text}
                }),
                
                CleanedFields = Table.TransformColumns(TrimmedFields,{
                    {"Group", Text.Clean, type text}, 
                    {"Employee", Text.Clean, type text}, 
                    {"Reason", Text.Clean, type text}, 
                    {"Status", Text.Clean, type text}
                }),
                
                // Clean Comments if column exists
                CleanedComments = if Table.HasColumns(CleanedFields, "Comments") 
                    then Table.TransformColumns(CleanedFields,{
                        {"Comments", each Text.Clean(Text.Trim(_)), type text}
                    })
                    else CleanedFields,
                
                // Add Year and Month for filtering
                AddedYear = Table.AddColumn(CleanedComments, "Year", each Date.Year([Date]), Int64.Type),
                AddedMonthName = Table.AddColumn(AddedYear, "Month Name", each Date.MonthName([Date]), type text)
            in
                AddedMonthName
        otherwise null
    ),
    
    // Remove failed files
    FilteredTransforms = Table.SelectRows(TransformFiles, each [Data] <> null),
    
    // Get column names from first successful file
    FirstFileColumns = Table.ColumnNames(FilteredTransforms{0}[Data]),
    ExpandedData = Table.ExpandTableColumn(FilteredTransforms, "Data", FirstFileColumns),
    CleanedColumns = Table.RemoveColumns(ExpandedData, {"Name", "Extension", "Date accessed", "Date modified", "Date created", "Attributes", "Folder Path", "Content"}, MissingField.Ignore)
in
    CleanedColumns